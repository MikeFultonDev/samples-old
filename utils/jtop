#
# jtop: list the jobs in a simplistic 'top' like format (sorted by CT column but can be sorted by any column by piping through sort)
#
orig=`opercmd '$d condef' | 
	awk ' { 
		ind = index($0, "DISPMAX="); 
		if (ind > 0) { 
			result=substr($0,ind+8); 
			ind = index(result, ","); 
			if (ind > 0) { 
				result=substr(result,ind-1); 
			} 
			print result;
		}
	}'
     `

opercmd '$t condef,dispmax=1000000' >/dev/null 2>&1
opercmd 'd jobs,all' >/tmp/out
# set the condef back again...
opercmd "\$t condef,dispmax=${orig}" >/dev/null 2>&1
#
#Sample raw output:
#
#S0W1      2018005  11:09:37.20             IEE115I 11.09.37 2018.005 ACTIVITY 846
#                                            JOBS     M/S    TS USERS    SYSAS    INITS   ACTIVE/MAX VTAM     OAS
#                                           00018    00030    00000      00033    00030    00000/00040       00038
#                                            *MASTER* *MASTER*          NSW  *   A=0001   PER=NO   SMC=000
#                                                                                PGN=N/A  DMN=N/A  AFF=NONE
#                                                                                CT=00.18.12  ET=00402.40
#                                                                                WUID=STC03388 USERID=+MASTER+
#                                                                                WKL=SYSTEM   SCL=SYSTEM   P=1
#                                                                                RGP=N/A      SRVR=NO  QSC=NO
#                                            PCAUTH   PCAUTH            NSW  *   A=0002   PER=NO   SMC=000
#                                                                                PGN=N/A  DMN=N/A  AFF=NONE
#                                                                                CT=000.010S  ET=00402.40
#                                                                                WKL=SYSTEM   SCL=SYSTEM   P=1
#                                                                                RGP=N/A      SRVR=NO  QSC=NO
#
#Fields:
#List of active jobs for the specific name:
# Jobname, APPC/MVS transaction program name, initiator address space name
# Stepname
# Procedure stepname or requesting userid
# Type of job
# Address space identifier
# A=<...> Address space status
# PER=<...> Program event recording (PER) activity
# SMC=<...> Number of outstanding step-must-complete requests
# AFF=<...> Processor affinity
# ET=<...> Elapsed time since initiation
# Accumulated processor time
# WUID=<...> Work unit identifier
# USERID=<...> Transaction requestor's userid
# Central (real) address range (V=R only)

cat /tmp/out | awk '
function ltrim(s) { sub(/^[ \t\r\n]+/, "", s); return s }
function rtrim(s) { sub(/[ \t\r\n]+$/, "", s); return s }
function trim(s)  { return rtrim(ltrim(s)); }
BEGIN { inHeader=0; firstLine=0; inLine=0; count=0; blank=" "; dot="."; altct="CT>"; altet="ET>"; }
       /^[ ]+JOBS[ ]+M.S[ ]+TS USERS[ ]+SYSAS[ ]+INITS[ ]+ACTIVE.MAX VTAM[ ]+OAS/ { inHeader=1; next; }
       /^[ ]+PGN=/ { second=trim($0); next; }
       /^[ ]+CT=/  	{ 
				third=trim($0); 
				if (substr(third,11,1) != "S") {
					third=altct substr(third,4);
				}
				if (substr(third,19,1) == "." || substr(third,17,8) == "NOTAVAIL") {
					third=substr(third,1,13) altet substr(third,17);
				}
				next; 
			}
       /^[ ]+WUID=/  	{ 
				fourth=trim($0); 
				fourth=sprintf("%-29s", fourth)
				if (substr(fourth,15,1) == " ") {
					fourth=substr(fourth,1,14) dot substr(fourth,16)
				}
				next; 
			}
       /^[ ]+WKL=/  { fifth=trim($0); next; }
       /^[ ]+RGP=/  { sixth=trim($0); firstLine=1; print first blank second blank third blank fourth blank fifth blank sixth; next; }
       /^[ ]+/ 	{ 
			if (inHeader) { 
				jobInfo=$0; inHeader=0; firstLine=1; next; 
			} else if (firstLine) { 
				first=trim($0); 
				if (substr(first,20,1) == " ") { 
					first=substr(first,1,18) dot substr(first,20); 
				} 
				inLine=1; firstLine=0; next; 
			}
		}
     ' | sort -b -r -k 12.1
